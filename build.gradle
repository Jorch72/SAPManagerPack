buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "1.7.10-2.5.1"
group = "de.sanandrew.core"
archivesBaseName = 'SAPManPack'

minecraft {
	version = "1.7.10-10.13.3.1401-1710ls"
    runDir = "minecraft"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main {
        java { srcDir 'java' }
        resources { srcDir 'resources' }
    }
}

repositories {
    maven {
        name = 'CodeChicken'
        url = 'http://chickenbones.net/maven/'
    }
}

dependencies {
    compile 'codechicken:CodeChickenCore:1.7.10-1.0.4.35:dev'
    compile 'codechicken:CodeChickenLib:1.7.10-1.1.3.127:dev'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

jar {
    manifest {
        attributes 'FMLCorePlugin': 'de.sanandrew.core.manpack.init.ManPackLoadingPlugin'
        attributes 'FMLCorePluginContainsFMLMod' : 'true'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
    archiveName = "${baseName}-${version}-${appendix}.jar"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
    archiveName = "${baseName}-${version}-${appendix}.jar"
    manifest {
        attributes 'FMLCorePlugin': 'de.sanandrew.core.manpack.init.ManPackLoadingPlugin'
        attributes 'FMLCorePluginContainsFMLMod' : 'true'
    }
}

configurations {
    deployDev
}

artifacts {
    archives deobfJar
    archives sourceJar
    archives jar
    deployDev deobfJar
}

project.ext {
    releaseRepoUrl = "file://${project(':').projectDir}/maven-repo/"
}

uploadDeployDev {
    dependsOn("deobfJar")
    configuration = configurations.deployDev
    repositories.mavenDeployer {
        pom.artifactId = project.name
        pom.version = project.version + '-deobf'
        repository(url: project.releaseRepoUrl)
    }
}
